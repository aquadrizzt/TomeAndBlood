// -----------------------------
// REVISED SPECIALISTS
// -----------------------------
INCLUDE ~TomeAndBlood/lib/fl#add_kit_ee.tpa~
INCLUDE ~TomeAndBlood/lib/spell_to_innate.tpa~
// This component uses code by Subtledoctor; all such code is used with permission of the original author. 

ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN // version without Spell Revisions

	INCLUDE ~TomeAndBlood/data/core/spell_list_base.tpa~

	INCLUDE ~TomeAndBlood/data/core/scroll_list_base.tpa~ 
END 
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN // version with Spell Revisions

	INCLUDE ~TomeAndBlood/data/core/spell_list_sr.tpa~

	INCLUDE ~TomeAndBlood/data/core/scroll_list_sr.tpa~ 
END 

//Hiding NRD 
APPEND ~HIDESPL.2da~ ~SPWI124	1	0	0~ //add NRD to hidespl
	BUT_ONLY 
	
//New Descriptions 
COPY_EXISTING - ~kitlist.2da~ ~override~
	COUNT_2DA_COLS num_cols
		READ_2DA_ENTRIES_NOW ~r2en_kitlist~ num_cols
		FOR (row = 1; row < r2en_kitlist; row += 1) BEGIN
			READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 1 kitname
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~ABJURER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_abjurer
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~CONJURER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_conjurer
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~DIVINER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_diviner
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~ENCHANTER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_enchanter
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~ILLUSIONIST~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_illusionist
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~INVOKER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_invoker
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~NECROMANCER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_necromancer
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~TRANSMUTER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_transmuter
			END
		END
		
	STRING_SET_EVALUATE ~%str_abjurer%~ @101 	
	STRING_SET_EVALUATE ~%str_conjurer%~ @102 
	STRING_SET_EVALUATE ~%str_diviner%~ @103	
	STRING_SET_EVALUATE ~%str_enchanter%~ @104
	STRING_SET_EVALUATE ~%str_illusionist%~ @105 	
	STRING_SET_EVALUATE ~%str_invoker%~ @106
	STRING_SET_EVALUATE ~%str_necromancer%~ @107 	
	STRING_SET_EVALUATE ~%str_transmuter%~ @108	
		
	
//New CLAB Files
COPY ~TomeAndBlood/data/specialists/CLABMA02.2da~ ~override~ //abjurer
	 ~TomeAndBlood/data/specialists/CLABMA03.2da~ ~override~ //necromancer
	 ~TomeAndBlood/data/specialists/CLABMA04.2da~ ~override~ //transmuter
	 ~TomeAndBlood/data/specialists/CLABMA05.2da~ ~override~ //diviner
	 ~TomeAndBlood/data/specialists/CLABMA06.2da~ ~override~ //conjurer
	 ~TomeAndBlood/data/specialists/CLABMA07.2da~ ~override~ //invoker
	 ~TomeAndBlood/data/specialists/CLABMA08.2da~ ~override~ //illusionist
	 ~TomeAndBlood/data/specialists/CLABMA09.2da~ ~override~ //enchanter
	 
//Update Necromancer Alignments
COPY_EXISTING ~alignmnt.2da~ ~override~
	REPLACE_TEXTUALLY ~^NECROMANCER[ %TAB%]+.+~     ~NECROMANCER     0 0 1 0 0 1 0 0 1~
    PRETTY_PRINT_2DA
    BUT_ONLY 
		
//Updating Spells and Scrolls 
ACTION_PHP_EACH arcanespell AS ind => res BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%ind%.spl~) BEGIN 
		COPY_EXISTING ~%ind%.spl~ ~override~ 	
			WRITE_LONG 0x1e ("0x00000000") //blocked to: none
			ACTION_IF (res = 8) BEGIN //necromancy
				APPEND ~HIDESPL.2da~ ~%ind%	1	0	0~ //add necromancy to hidespl
				BUT_ONLY  
			END
	END
END
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF ~SOURCE_SIZE~ >0x71 BEGIN
		READ_SHORT 0x1c ~type~
		PATCH_IF ~type~=11 BEGIN // only check scrolls
			GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x38
			PHP_EACH ab_array AS int => ab_off BEGIN
				CLEAR_ARRAY fx_array
				GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
				PHP_EACH fx_array AS int => fx_off BEGIN
					READ_SHORT fx_off fx_type
					PATCH_IF fx_type = 147 BEGIN
						READ_ASCII fx_off + 0x14 spell
						SPRINT $scrollmap(~%spell%~) ~%SOURCE_RES%~
					END
				END
			END
		END
	END
BUT_ONLY
ACTION_PHP_EACH scrollmap AS yo => ko BEGIN
	COPY_EXISTING ~%yo%.spl~ ~override~
		READ_BYTE 0x0025 school
	BUT_ONLY
	COPY_EXISTING ~%ko%.itm~ ~override~
		WRITE_BYTE 0x2d ("0x00") //blocked to: none
		WRITE_BYTE 0x2f ("0x00") //blocked to: none
		PATCH_IF (%school% = 7) BEGIN // necromancy
			LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 147 END
		END
	BUT_ONLY
END

//Automatic School Spells 
COPY ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ABJ.SPL~ //making the new abjuration spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ALT.SPL~ //making the new alteration spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+CON.SPL~ //making the new conjuration spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+EVO.SPL~ //making the new evocation spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ENC.SPL~ //making the new encahntment spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ILL.SPL~ //making the new illusion spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+NEC.SPL~ //making the new necromancy spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+DIV.SPL~ //making the new divination spell 
	 
	 
ACTION_PHP_EACH arcanespell AS ind => res BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%ind%.spl~) BEGIN 
		ACTION_IF (~%ind%~ STRING_EQUAL ~SPWI123~ OR //Find Familiar
				   ~%ind%~ STRING_EQUAL ~SPWI117~ OR //Chill Touch
				   ~%ind%~ STRING_EQUAL ~SPWI218~ OR //Ghoul Touch
				   ~%ind%~ STRING_EQUAL ~SPWI314~ OR //Vampiric Touch
				   ~%ind%~ STRING_EQUAL ~SPWI124~ OR //NRD
				   ~%ind%~ STRING_EQUAL ~SPWI222~ OR //Chaos Shield
				   ~%ind%~ STRING_EQUAL ~SPWI723~ OR //Improved Chaos Shield
				   ~%ind%~ STRING_EQUAL ~SPWI406~ OR //Minor Globe of Invulnerability
				   ~%ind%~ STRING_EQUAL ~SPWI522~ OR //Minor Spell Turning
				   ~%ind%~ STRING_EQUAL ~SPWI602~ OR //Globe of Invulnerability
				   ~%ind%~ STRING_EQUAL ~SPWI618~ OR //Spell Deflection
				   ~%ind%~ STRING_EQUAL ~SPWI701~ OR //Spell Turning
				   ~%ind%~ STRING_EQUAL ~SPWI902~ OR //Spell Trap
				   (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ AND  // Spell Revisions
					   (~%ind%~ STRING_EQUAL ~SPWI226~ OR //SR: MS 1
					   ~%ind%~ STRING_EQUAL ~SPWI309~ OR //SR: MS 2
					   ~%ind%~ STRING_EQUAL ~SPWI423~ OR //SR: MS 3
					   ~%ind%~ STRING_EQUAL ~SPWI504~ OR //SR: MS 4
					   ~%ind%~ STRING_EQUAL ~SPWI619~ OR //SR: MS 5
					   ~%ind%~ STRING_EQUAL ~SPWI724~)	   
				   ) OR 
				   ((NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) AND //No Spell Revisions
					   (~%ind%~ STRING_EQUAL ~SPWI309~ OR //Monster Summoning I
					   ~%ind%~ STRING_EQUAL ~SPWI407~ OR //Monster Summoning II
					   ~%ind%~ STRING_EQUAL ~SPWI504~) //Monster Summoning III
				   ) 
				   OR //Spell Trap
				   (GAME_IS ~bgee~ AND  // Dancing Lights in BGEE
					   (~%ind%~ STRING_EQUAL ~SPWI126~))
				   ) BEGIN //exclusion list here 
			//do nothing
		END 	
		ELSE BEGIN 
		COPY_EXISTING ~%ind%.spl~ ~override~		
			READ_BYTE 0x25 school
		ACTION_IF (school = 1) BEGIN //abjuration
				COPY_EXISTING ~QDMG+ABJ.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 8) BEGIN //alteration
				COPY_EXISTING ~QDMG+ALT.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 2) BEGIN //conjuration
				COPY_EXISTING ~QDMG+CON.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 3) BEGIN //divination
				COPY_EXISTING ~QDMG+DIV.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 4) BEGIN //enchantment
				COPY_EXISTING ~QDMG+ENC.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 6) BEGIN //evocation
				COPY_EXISTING ~QDMG+EVO.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 5) BEGIN //illusion
				COPY_EXISTING ~QDMG+ILL.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END
			
			ACTION_IF (school = 7) BEGIN //necromancy
				COPY_EXISTING ~QDMG+NEC.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%ind%~ END 
			END	
		END 
	END 
END 
//Base Abilities and CLAB Updates
COPY ~TomeAndBlood/data/specialists/D5_TRAN1.SPL~ ~override~ //transmuter poly/petrify immunity
	 ~TomeAndBlood/data/specialists/D5_DIVI1.SPL~ ~override~ //diviner backstab immunity
	 ~TomeAndBlood/data/specialists/D5_ENC1.SPL~ ~override~ //enchanter charm/sleep immu
	 ~TomeAndBlood/data/specialists/D5_ENC2.SPL~ ~override~ //enchanter aura
	 ~TomeAndBlood/data/specialists/D5_ENC2.EFF~ ~override~ //enchanter aura effect
	 ~TomeAndBlood/data/specialists/D5_ENC2A.SPL~ ~override~ //enchanter aura spl
	 
	 
//Abjurer Spell Prot EEs 
ACTION_IF GAME_IS ~bgee bg2ee iwdee eet~ THEN BEGIN 
	
	//Spell Shield for Abjurers
	COPY ~TomeAndBlood/data/specialists/d5_abju.eff~ ~override~
	COPY_EXISTING ~spwi522.spl~ ~override~
			~spwi618.spl~ ~override~
			~spwi701.spl~ ~override~
			~spwi902.spl~ ~override~
			~spwi406.spl~ ~override~
			~spwi602.spl~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 parameter1=64 parameter2=9 resist_dispel=0 timing=9 power=0 STR_VAR resource=~d5_abju~ END
	
	//Add new abilities to Bonus Spells
	COPY_EXISTING ~QDMG+ABJ.SPL~ ~override~ 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi406~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi522~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi602~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi618~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi701~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi902~ END 	

END 
ELSE BEGIN 
	//ToB Abjurer updates
	ACTION_IF ENGINE_IS ~tob~ THEN BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
			COPY_EXISTING ~spwi406.spl~ ~override/d5_abju1.spl~
				SAY NAME1 @6201
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi522.spl~ ~override/d5_abju2.spl~
				SAY NAME1 @6202
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi602.spl~ ~override/d5_abju3.spl~
				SAY NAME1 @6203
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi618.spl~ ~override/d5_abju4.spl~
				SAY NAME1 @6204
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi701.spl~ ~override/d5_abju5.spl~
				SAY NAME1 @6205
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi902.spl~ ~override/d5_abju6.spl~
				SAY NAME1 @6206
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
			COPY_EXISTING ~spwi406.spl~ ~override/d5_abju1.spl~
				SAY NAME1 @6211
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi522.spl~ ~override/d5_abju2.spl~
				SAY NAME1 @6212
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi602.spl~ ~override/d5_abju3.spl~
				SAY NAME1 @6213
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi618.spl~ ~override/d5_abju4.spl~
				SAY NAME1 @6214
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi701.spl~ ~override/d5_abju5.spl~
				SAY NAME1 @6215
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi902.spl~ ~override/d5_abju6.spl~
				SAY NAME1 @6216
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi406.spl~ THEN BEGIN
			COPY_EXISTING ~spwi406.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi522.spl~ THEN BEGIN
			COPY_EXISTING ~spwi522.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi602.spl~ THEN BEGIN
			COPY_EXISTING ~spwi602.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi618.spl~ THEN BEGIN
			COPY_EXISTING ~spwi618.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi701.spl~ THEN BEGIN
			COPY_EXISTING ~spwi701.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi902.spl~ THEN BEGIN
			COPY_EXISTING ~spwi902.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		
		//Add new abilities to Bonus Spells
		COPY_EXISTING ~QDMG+ABJ.SPL~ ~override~ 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU1~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU2~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU3~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU4~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU5~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU6~ END 
	END
END 

//Conjurer with SR 
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN //SR

	//Reducing Monster Summoning Spells by 1 level 
	COPY_EXISTING ~spwi226.spl~ ~override/d5_conj1.spl~
		WRITE_LONG 0x34 1
	COPY_EXISTING ~spwi309.spl~ ~override/d5_conj2.spl~
		WRITE_LONG 0x34 2
	COPY_EXISTING ~spwi423.spl~ ~override/d5_conj3.spl~
		WRITE_LONG 0x34 3
	COPY_EXISTING ~spwi504.spl~ ~override/d5_conj4.spl~
		WRITE_LONG 0x34 4
	COPY_EXISTING ~spwi619.spl~ ~override/d5_conj5.spl~
		WRITE_LONG 0x34 5
	COPY_EXISTING ~spwi724.spl~ ~override/d5_conj6.spl~
		WRITE_LONG 0x34 6
		
	//Add new abilities to Bonus Spells
	COPY_EXISTING ~QDMG+CON.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ1~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ2~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ3~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ4~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ5~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ6~ END 
	
	//Updating description
	ACTION_GET_STRREF str_conjurer conjurer
	OUTER_SPRINT old @110
	OUTER_SPRINT new @112
	OUTER_PATCH_SAVE conjurer ~%conjurer%~ BEGIN 
		REPLACE_TEXTUALLY ~%old%~ ~%new%~
	END 
	STRING_SET_EVALUATE str_conjurer ~%conjurer%~
	
END 
ELSE BEGIN //No SR 

	//Making Monster Summoning I, II, III innates
	COPY_EXISTING ~spwi309.spl~ ~override/d5_conv1.spl~
		LPM spell_to_innate
	COPY_EXISTING ~spwi407.spl~ ~override/d5_conv2.spl~
		LPM spell_to_innate
	COPY_EXISTING ~spwi504.spl~ ~override/d5_conv3.spl~
		LPM spell_to_innate
	
	APPEND ~CLABMA06.2da~ ~MON_SUMM 		****        GA_D5_CONV1 ****        GA_D5_CONV1 ****        GA_D5_CONV2 ****        GA_D5_CONV2 ****        GA_D5_CONV3~ 
	
	//Description Updates
	ACTION_GET_STRREF str_conjurer conjurer
	OUTER_SPRINT old @110
	OUTER_SPRINT new @111
	OUTER_PATCH_SAVE conjurer ~%conjurer%~ BEGIN 
		REPLACE_TEXTUALLY ~%old%~ ~%new%~
	END 
	STRING_SET_EVALUATE str_conjurer ~%conjurer%~
	
	//adding skipped abilities to the bonus spells
	COPY_EXISTING ~QDMG+CON.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~SPWI309~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~SPWI407~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~SPWI504~ END 
END 

//Invoker EE 
ACTION_IF (GAME_IS ~iwdee~ OR FILE_EXISTS_IN_GAME ~enginest.2da~) THEN BEGIN //iwdee 1.4+, Engine v2.0+

	//+20% Damage Bonus
	COPY ~TomeAndBlood/data/specialists/d5_pyroe.spl~ ~override/d5_invo1.spl~
	
	APPEND ~CLABMA07.2da~ ~DMGBONUS 		AP_D5_INVO1~
	
	//Description Update
	ACTION_GET_STRREF str_invoker invoker
	OUTER_SPRINT old @113
	OUTER_SPRINT new @114
	OUTER_PATCH_SAVE invoker ~%invoker%~ BEGIN 
		REPLACE_TEXTUALLY ~%old%~ ~%new%~
	END 
	STRING_SET_EVALUATE str_invoker ~%invoker%~
END 

//Illusioninst non-EE 
ACTION_IF ENGINE_IS ~tob~ THEN BEGIN //shadow step if not EEs
	COPY ~TomeAndBlood/data/specialists/spsd02.spl~ ~override~
		SAY NAME1 @6049
		SAY UNIDENTIFIED_DESC @6050
	COPY ~TomeAndBlood/data/specialists/spsd02b.bam~ ~override~
	COPY ~TomeAndBlood/data/specialists/spsd02c.bam~ ~override~
END 

//Ilusionist SR 
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN 
	COPY ~TomeAndBlood/data/specialists/d5_illus.spl~ ~override/d5_illu1.spl~
END 
ELSE BEGIN 
	COPY ~TomeAndBlood/data/specialists/d5_illuv.spl~ ~override/d5_illu1.spl~
END 

//Necromancer Character Creation Spells
COPY_EXISTING ~spwi110.spl~ ~override/QDNEC#1.spl~ 

ADD_SPELL  ~override/QDNEC#1.spl~ 2 1 TNB_NECRO_CC_I 
	SAY NAME1 @116 
	SAY UNIDENTIFIED_DESC @115
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers (and wildmages)
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 2 TNB_NECRO_CC_II 
	SAY NAME1 @117 
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 2
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 3 TNB_NECRO_CC_III
	SAY NAME1 @118
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 3
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 4 TNB_NECRO_CC_IV 
	SAY NAME1 @119 
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 4
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 

ADD_SPELL  ~override/QDNEC#1.spl~ 2 5 TNB_NECRO_CC_V 
	SAY NAME1 @120
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 5
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers (and wildmages)
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 6 TNB_NECRO_CC_VI 
	SAY NAME1 @121 
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 6
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 7 TNB_NECRO_CC_VII
	SAY NAME1 @122
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 7
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 8 TNB_NECRO_CC_VIII
	SAY NAME1 @123
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 8
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers

ADD_SPELL  ~override/QDNEC#1.spl~ 2 9 TNB_NECRO_CC_IX
	SAY NAME1 @124
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 9
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers (and wildmages)
	
	
//Remove Char Creation spells from Necro/Wild Mage 
//Create new spell to remove those 9 spells 
COPY ~TomeAndBlood/data/specialists/QDNEC#.spl~ ~override~

ACTION_FOR_EACH necspl IN ~TNB_NECRO_CC_I~ ~TNB_NECRO_CC_II~ ~TNB_NECRO_CC_III~ ~TNB_NECRO_CC_IV~ ~TNB_NECRO_CC_V~ ~TNB_NECRO_CC_VI~ ~TNB_NECRO_CC_VII~ ~TNB_NECRO_CC_VIII~ ~TNB_NECRO_CC_IX~ BEGIN 
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = EVAL ~%necspl%~ 
		RET spell_res
	END
	COPY_EXISTING ~QDNEC#.spl~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource= EVAL ~%spell_res%~ END  
END 

//also fixes wild mage, not destructively
APPEND ~CLABMA01.2da~ ~SPEC_FIX	 	AP_QDNEC#~
	
