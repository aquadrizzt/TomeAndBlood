// -----------------------------
// REVISED SPECIALISTS
// -----------------------------
INCLUDE ~TomeAndBlood/lib/fl#add_kit_ee.tpa~
INCLUDE ~TomeAndBlood/lib/spell_to_innate.tpa~
INCLUDE ~TomeAndBlood/lib/add_spell_header.tpa~
// This component uses code by Subtledoctor; all such code is used with permission of the original author. 

// spontaneous casting icons
COPY ~TomeAndBlood/data/specialists/d5spnt1.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt2.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt3.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt4.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt5.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt6.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt7.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt8.bam~ ~override~
	~TomeAndBlood/data/specialists/d5spnt9.bam~ ~override~

COPY ~TomeAndBlood/data/specialists/d5spont.spl~ ~override~
	SAY NAME1 @6005
	SAY UNIDENTIFIED_DESC @6005
COPY ~TomeAndBlood/data/specialists/d5spntx.spl~ ~override~
	SAY NAME1 @6006
	SAY UNIDENTIFIED_DESC @6006
COPY ~TomeAndBlood/data/specialists/d5spnt.bam~ ~override~
COPY ~TomeAndBlood/data/specialists/d5spnt0.bam~ ~override~

ACTION_FOR_EACH school IN ~1~ ~2~ ~3~ ~4~ ~5~ ~6~ ~7~ ~8~ BEGIN
  ACTION_FOR_EACH level IN ~1~ ~2~ ~3~ ~4~ ~5~ ~6~ ~7~ ~8~ ~9~ BEGIN
	COPY ~TomeAndBlood/data/specialists/spntlist.2da~ ~override/d5spnt%school%%level%.2da~
	COPY ~TomeAndBlood/data/specialists/d5spont.eff~ ~override/d5spnt%school%%level%.eff~
	  WRITE_EVALUATED_ASCII 0x30 ~d5spnt%school%%level%~ #8
  END
END

/*
	Note:
Abjuration = 1
Conjuration = 2
Divination = 3
Enchantment = 4
Illusion = 5
Invocation = 6
Necromancy = 7
Transmutation = 8
*/

OUTER_SET count = 0
//OUTER_SPRINT $wizard_spells("%count%") "%spell%"

COPY_EXISTING_REGEXP GLOB ~^SPWI[1-9][0-5][0-9].spl$~ ~override~
  READ_SHORT 0x1c type
  PATCH_IF type = 1 BEGIN
//	PATCH_IF NOT FILE_CONTAINS_EVALUATED (~hidespl.2da~ ~%SOURCE_RES%~) BEGIN
	  INNER_PATCH_SAVE res_num ~%SOURCE_RES%~ BEGIN
		REPLACE_TEXTUALLY ~^[Ss][Pp][Ww][Ii]~ ~2~
	  END
	  PATCH_IF FILE_CONTAINS_EVALUATED (~spell.ids~ ~%res_num%~) BEGIN
	    PATCH_PRINT "%SOURCE_RES%"
		SPRINT $wizard_spells("%count%") "%SOURCE_RES%"
		++count
	  END
//	END
  END
BUT_ONLY

CREATE EFF ~d5spont~
    WRITE_LONG 0x10 171
    WRITE_LONG 0x14 1
    WRITE_LONG 0x24 9
    WRITE_SHORT 0x2c 100
    WRITE_ASCII 0x30 ~d5spont~ #8
    
ACTION_PHP_EACH wizard_spells AS ind => wizard_spell BEGIN
  OUTER_INNER_PATCH_SAVE spont_res ~%wizard_spell%~ BEGIN
	REPLACE_TEXTUALLY ~^[Ss][Pp][Ww][Ii]~ ~d5wy~
  END
  ACTION_IF FILE_EXISTS_IN_GAME ~%wizard_spell%.spl~ BEGIN 
	COPY_EXISTING ~%wizard_spell%.spl~ ~override/%spont_res%.spl~
	  READ_SHORT 0x1c spell_type
	  READ_LONG 0x34 spell_level
	  READ_BYTE 0x25 spell_school
	  PATCH_IF (spell_type = 1) BEGIN
		READ_LONG 0x64 abil_offset
		READ_SHORT 0x68 abil_number
		READ_BYTE (%abil_offset% + 0x0c) abil_target
		READ_SHORT (%abil_offset% + 0x0e) abil_range
		READ_LONG 0x6a eff_offset
		WHILE (%abil_number% > 0) BEGIN
		  SET abil_number = (%abil_number% - 1)
		  WRITE_SHORT (%abil_offset% + 0x26 + (0x28 * %abil_number%)) 1
		END
		READ_BYTE (%eff_offset% + 0x02) eff_target
		LPF DELETE_EFFECT INT_VAR match_probability2 = 0 END
		PATCH_IF (%abil_target% = 4) BEGIN
		  LPF ADD_SPELL_EFFECT INT_VAR opcode = 148 target = 1 power = %spell_level% parameter1 = 0 parameter2 = 1 timing = 1 STR_VAR resource = EVAL ~%wizard_spell%~ END
		  PATCH_IF (%abil_range% < 35) BEGIN
			PATCH_IF (%abil_range% > 4) BEGIN
			  LPF ALTER_SPELL_HEADER INT_VAR range = (%abil_range% - 3) END
			END
			PATCH_IF (%abil_range% < 5) BEGIN
			  LPF ALTER_SPELL_HEADER INT_VAR target = 5 END	// self? better than just making it short-range?
 //			  LPF ALTER_SPELL_HEADER INT_VAR range = 2 END	// alternatively
			END
		  END
		END
		ELSE BEGIN
		  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = %eff_target% power = %spell_level% parameter1 = 0 parameter2 = 1 timing = 1 STR_VAR resource = EVAL ~%wizard_spell%~ END
		END
	  END
	  INNER_ACTION BEGIN
	  	ACTION_IF FILE_EXISTS_IN_GAME ~d5spnt%spell_school%%spell_level%.2da~ BEGIN
		  APPEND ~d5spnt%spell_school%%spell_level%.2da~ ~%wizard_spell%	%spont_res%	3 ~
		END
	  END
	BUT_ONLY
	COPY_EXISTING ~%wizard_spell%.spl~ ~override~
	  READ_SHORT 0x1c spell_type
	  READ_LONG 0x34 spell_level
	  READ_BYTE 0x25 spell_school
	  READ_SHORT 0x68 abil_number
 	  LPF ADD_SPELL_HEADER INT_VAR required_level = 51 location = 2 target = 7 speed = 0 projectile = 1 STR_VAR icon = EVAL ~d5spnt%spell_level%~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR header = (%abil_number% + 1) opcode = 321 target = 1 parameter2 = 0 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_EFFECT INT_VAR header = (%abil_number% + 1) opcode = 144 target = 1 parameter2 = 2 timing = 0 duration = 5 END
	  LPF ADD_SPELL_EFFECT INT_VAR header = (%abil_number% + 1) opcode = 144 target = 1 parameter2 = 3 timing = 0 duration = 5 END
	  LPF ADD_SPELL_EFFECT INT_VAR header = (%abil_number% + 1) opcode = 144 target = 1 parameter2 = 4 timing = 0 duration = 5 END
	  LPF ADD_SPELL_EFFECT INT_VAR header = (%abil_number% + 1) opcode = 144 target = 1 parameter2 = 5 timing = 0 duration = 5 END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 64 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 128 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 256 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 512 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 1024 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 2048 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 4096 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 9 parameter1 = 8192 timing = 9 STR_VAR resource = ~d5spont~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 172 target = 1 timing = 1 STR_VAR resource = ~d5spntx~ END
	  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 172 target = 1 timing = 1 STR_VAR resource = ~d5spont~ END
	  PATCH_FOR_EACH specialist IN ~64~ ~128~ ~256~ ~512~ ~1024~ ~2048~ ~4096~ ~8192~ BEGIN
		PATCH_IF specialist = 64 BEGIN
		  SET spec_school = 1
	  	END
		PATCH_IF specialist = 128 BEGIN
		  SET spec_school = 2
	  	END
		PATCH_IF specialist = 256 BEGIN
		  SET spec_school = 3
	  	END
		PATCH_IF specialist = 512 BEGIN
		  SET spec_school = 4
	  	END
		PATCH_IF specialist = 1024 BEGIN
		  SET spec_school = 5
	  	END
		PATCH_IF specialist = 2048 BEGIN
		  SET spec_school = 6
	  	END
		PATCH_IF specialist = 4096 BEGIN
		  SET spec_school = 7
	  	END
		PATCH_IF specialist = 8192 BEGIN
		  SET spec_school = 8
	  	END
		LPF ADD_SPELL_EFFECT INT_VAR header = (%abil_number% + 1) opcode = 177 parameter2 = 9 parameter1 = %specialist% target = 1 timing = 1 STR_VAR resource = EVAL ~d5spnt%spec_school%%spell_level%~ END
	  END
  END
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN // version without Spell Revisions
	INCLUDE ~TomeAndBlood/data/core/spell_list_base.tpa~
END
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN // version for Spell Revisions
	INCLUDE ~TomeAndBlood/data/core/spell_list_sr.tpa~
END

//Hiding NRD 
APPEND ~HIDESPL.2da~ ~SPWI124	1	0	0~ //add NRD to hidespl
	BUT_ONLY 
	
//New Descriptions 
COPY_EXISTING - ~kitlist.2da~ ~override~
//	COUNT_2DA_COLS num_cols
		READ_2DA_ENTRIES_NOW ~r2en_kitlist~ 9
		FOR (row = 1; row < r2en_kitlist; row += 1) BEGIN
			READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 1 kitname
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~ABJURER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_abjurer
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~CONJURER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_conjurer
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~DIVINER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_diviner
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~ENCHANTER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_enchanter
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~ILLUSIONIST~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_illusionist
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~INVOKER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_invoker
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~NECROMANCER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_necromancer
			END
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~TRANSMUTER~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 str_transmuter
			END
		END
		
	STRING_SET_EVALUATE ~%str_abjurer%~ @101 	
	STRING_SET_EVALUATE ~%str_conjurer%~ @102 
	STRING_SET_EVALUATE ~%str_diviner%~ @103	
	STRING_SET_EVALUATE ~%str_enchanter%~ @104
	STRING_SET_EVALUATE ~%str_illusionist%~ @105 	
	STRING_SET_EVALUATE ~%str_invoker%~ @106
	STRING_SET_EVALUATE ~%str_necromancer%~ @107 	
	STRING_SET_EVALUATE ~%str_transmuter%~ @108	
		
	
//New CLAB Files
COPY ~TomeAndBlood/data/specialists/CLABMA02.2da~ ~override~ //abjurer
	 ~TomeAndBlood/data/specialists/CLABMA03.2da~ ~override~ //necromancer
	 ~TomeAndBlood/data/specialists/CLABMA04.2da~ ~override~ //transmuter
	 ~TomeAndBlood/data/specialists/CLABMA05.2da~ ~override~ //diviner
	 ~TomeAndBlood/data/specialists/CLABMA06.2da~ ~override~ //conjurer
	 ~TomeAndBlood/data/specialists/CLABMA07.2da~ ~override~ //invoker
	 ~TomeAndBlood/data/specialists/CLABMA08.2da~ ~override~ //illusionist
	 ~TomeAndBlood/data/specialists/CLABMA09.2da~ ~override~ //enchanter

/*
.
.
.
.
.	 
//Update Necromancer Alignments
COPY_EXISTING ~alignmnt.2da~ ~override~
	REPLACE_TEXTUALLY ~^NECROMANCER[ %TAB%]+.+~     ~NECROMANCER     0 0 1 0 0 1 0 0 1~
    PRETTY_PRINT_2DA
    BUT_ONLY 
.
.
.
.
.
*/
		
COPY_EXISTING ~qdscrlmap.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW ~r2en_schools~ cols
      FOR (row = 1; row < r2en_schools; row += 1) BEGIN
        READ_2DA_ENTRY_FORMER ~r2en_schools~ row 0 the_spell
        READ_2DA_ENTRY_FORMER ~r2en_schools~ row 1 the_scroll
        READ_2DA_ENTRY_FORMER ~r2en_schools~ row 2 the_school
		TEXT_SPRINT $d5_spell_scroll_school(~%the_spell%~ ~%the_scroll%~) ~%the_school%~
	END
BUT_ONLY

//Updating Spells and Scrolls 
ACTION_PHP_EACH d5_spell_scroll_school AS spell => school BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~) BEGIN 
		COPY_EXISTING ~%spell%.spl~ ~override~ 	
			WRITE_LONG 0x1e ("0x00000000") //blocked to: none
//		ACTION_IF (~%school%~ STRING_EQUAL_CASE ~necromancy~) BEGIN
//			APPEND ~HIDESPL.2da~ ~%spell%	1	0	0~ //add necromancy to hidespl
//		END
		ACTION_IF (FILE_EXISTS_IN_GAME ~%spell_1%.itm~) BEGIN 
			COPY_EXISTING ~%spell_1%.itm~ ~override~ 	
				WRITE_BYTE 0x2d ("0x00") //blocked to: none
				WRITE_BYTE 0x2f ("0x00") //blocked to: none
		END
	END
END

//Automatic School Spells 
COPY ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ABJ.SPL~ //making the new abjuration spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ALT.SPL~ //making the new alteration spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+CON.SPL~ //making the new conjuration spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+EVO.SPL~ //making the new evocation spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ENC.SPL~ //making the new encahntment spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+ILL.SPL~ //making the new illusion spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+NEC.SPL~ //making the new necromancy spell 
	 ~TomeAndBlood/data/specialists/QDMG+SCH.SPL~ ~override/QDMG+DIV.SPL~ //making the new divination spell 
	 
	 
ACTION_PHP_EACH d5_spell_scroll_school AS spell => school BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~) BEGIN 
		ACTION_IF (~%spell%~ STRING_EQUAL_CASE ~SPWI123~ OR //Find Familiar
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI117~ OR //Chill Touch
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI218~ OR //Ghoul Touch
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI314~ OR //Vampiric Touch
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI124~ OR //NRD
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI222~ OR //Chaos Shield
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI723~ OR //Improved Chaos Shield
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI406~ OR //Minor Globe of Invulnerability
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI522~ OR //Minor Spell Turning
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI602~ OR //Globe of Invulnerability
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI618~ OR //Spell Deflection
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI701~ OR //Spell Turning
				   ~%spell%~ STRING_EQUAL_CASE ~SPWI902~ OR //Spell Trap
				   (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ AND  // Spell Revisions
					   (~%spell%~ STRING_EQUAL_CASE ~SPWI226~ OR //SR: MS 1
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI309~ OR //SR: MS 2
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI423~ OR //SR: MS 3
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI504~ OR //SR: MS 4
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI619~ OR //SR: MS 5
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI724~)	   
				   ) OR 
				   ((NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) AND //No Spell Revisions
					   (~%spell%~ STRING_EQUAL_CASE ~SPWI309~ OR //Monster Summoning I
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI407~ OR //Monster Summoning II
					   ~%spell%~ STRING_EQUAL_CASE ~SPWI504~) //Monster Summoning III
				   ) 
				   OR //Spell Trap
				   (GAME_IS ~bgee~ AND  // Dancing Lights in BGEE
					   (~%spell%~ STRING_EQUAL_CASE ~SPWI126~))
				   ) BEGIN //exclusion list here 
			//do nothing
		END 	
		ELSE BEGIN 
			COPY_EXISTING ~%spell%.spl~ ~override~		
				READ_BYTE 0x25 school
			ACTION_IF (school = 1) BEGIN //abjuration
				COPY_EXISTING ~QDMG+ABJ.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 8) BEGIN //alteration
				COPY_EXISTING ~QDMG+ALT.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 2) BEGIN //conjuration
				COPY_EXISTING ~QDMG+CON.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 3) BEGIN //divination
				COPY_EXISTING ~QDMG+DIV.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 4) BEGIN //enchantment
				COPY_EXISTING ~QDMG+ENC.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 6) BEGIN //evocation
				COPY_EXISTING ~QDMG+EVO.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 5) BEGIN //illusion
				COPY_EXISTING ~QDMG+ILL.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END
			
			ACTION_IF (school = 7) BEGIN //necromancy
				COPY_EXISTING ~QDMG+NEC.SPL~ ~override~ 
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= EVAL ~%spell%~ END 
			END	
		END 
	END 
END 
//Base Abilities and CLAB Updates
COPY ~TomeAndBlood/data/specialists/D5_TRAN1.SPL~ ~override~ //transmuter poly/petrify immunity
	 ~TomeAndBlood/data/specialists/D5_DIVI1.SPL~ ~override~ //diviner backstab immunity
	 ~TomeAndBlood/data/specialists/D5_ENC1.SPL~ ~override~ //enchanter charm/sleep immu
	 ~TomeAndBlood/data/specialists/D5_ENC2.SPL~ ~override~ //enchanter aura
	 ~TomeAndBlood/data/specialists/D5_ENC2.EFF~ ~override~ //enchanter aura effect
	 ~TomeAndBlood/data/specialists/D5_ENC2A.SPL~ ~override~ //enchanter aura spl
	 
	 
//Abjurer Spell Prot EEs 
ACTION_IF GAME_IS ~bgee bg2ee iwdee eet~ THEN BEGIN 
	
	//Spell Shield for Abjurers
	COPY ~TomeAndBlood/data/specialists/d5_abju.eff~ ~override~
	COPY_EXISTING ~spwi522.spl~ ~override~
			~spwi618.spl~ ~override~
			~spwi701.spl~ ~override~
			~spwi902.spl~ ~override~
			~spwi406.spl~ ~override~
			~spwi602.spl~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 parameter1=64 parameter2=9 resist_dispel=0 timing=9 power=0 STR_VAR resource = ~d5_abju~ END
	
	//Add new abilities to Bonus Spells
	COPY_EXISTING ~QDMG+ABJ.SPL~ ~override~ 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi406~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi522~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi602~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi618~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi701~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~spwi902~ END 	

END 
ELSE BEGIN 
	//ToB Abjurer updates
	ACTION_IF ENGINE_IS ~tob~ THEN BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
			COPY_EXISTING ~spwi406.spl~ ~override/d5_abju1.spl~
				SAY NAME1 @6201
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi522.spl~ ~override/d5_abju2.spl~
				SAY NAME1 @6202
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi602.spl~ ~override/d5_abju3.spl~
				SAY NAME1 @6203
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi618.spl~ ~override/d5_abju4.spl~
				SAY NAME1 @6204
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi701.spl~ ~override/d5_abju5.spl~
				SAY NAME1 @6205
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi902.spl~ ~override/d5_abju6.spl~
				SAY NAME1 @6206
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
			COPY_EXISTING ~spwi406.spl~ ~override/d5_abju1.spl~
				SAY NAME1 @6211
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi522.spl~ ~override/d5_abju2.spl~
				SAY NAME1 @6212
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi602.spl~ ~override/d5_abju3.spl~
				SAY NAME1 @6213
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi618.spl~ ~override/d5_abju4.spl~
				SAY NAME1 @6214
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi701.spl~ ~override/d5_abju5.spl~
				SAY NAME1 @6215
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
			COPY_EXISTING ~spwi902.spl~ ~override/d5_abju6.spl~
				SAY NAME1 @6216
				LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 resist_dispel=0 timing=0 power=0 STR_VAR resource=~spwi519~ END
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi406.spl~ THEN BEGIN
			COPY_EXISTING ~spwi406.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi522.spl~ THEN BEGIN
			COPY_EXISTING ~spwi522.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi602.spl~ THEN BEGIN
			COPY_EXISTING ~spwi602.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi618.spl~ THEN BEGIN
			COPY_EXISTING ~spwi618.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi701.spl~ THEN BEGIN
			COPY_EXISTING ~spwi701.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~spwi902.spl~ THEN BEGIN
			COPY_EXISTING ~spwi902.spl~ ~override~
				WRITE_LONG 0x1e ("0x00000040") //blocked: abjurers
				BUT_ONLY
		END
		
		//Add new abilities to Bonus Spells
		COPY_EXISTING ~QDMG+ABJ.SPL~ ~override~ 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU1~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU2~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU3~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU4~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU5~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_ABJU6~ END 
	END
END 

//Conjurer with SR 
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN //SR

	//Reducing Monster Summoning Spells by 1 level 
	COPY_EXISTING ~spwi226.spl~ ~override/d5_conj1.spl~
		WRITE_LONG 0x34 1
	COPY_EXISTING ~spwi309.spl~ ~override/d5_conj2.spl~
		WRITE_LONG 0x34 2
	COPY_EXISTING ~spwi423.spl~ ~override/d5_conj3.spl~
		WRITE_LONG 0x34 3
	COPY_EXISTING ~spwi504.spl~ ~override/d5_conj4.spl~
		WRITE_LONG 0x34 4
	COPY_EXISTING ~spwi619.spl~ ~override/d5_conj5.spl~
		WRITE_LONG 0x34 5
	COPY_EXISTING ~spwi724.spl~ ~override/d5_conj6.spl~
		WRITE_LONG 0x34 6
		
	//Add new abilities to Bonus Spells
	COPY_EXISTING ~QDMG+CON.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ1~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ2~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ3~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ4~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ5~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~D5_CONJ6~ END 
	
	//Updating description
	ACTION_GET_STRREF str_conjurer conjurer
	OUTER_SPRINT old @110
	OUTER_SPRINT new @112
	OUTER_PATCH_SAVE conjurer ~%conjurer%~ BEGIN 
		REPLACE_TEXTUALLY ~%old%~ ~%new%~
	END 
	STRING_SET_EVALUATE str_conjurer ~%conjurer%~
	
END 
ELSE BEGIN //No SR 

	//Making Monster Summoning I, II, III innates
	COPY_EXISTING ~spwi309.spl~ ~override/d5_conv1.spl~
		LPM spell_to_innate
	COPY_EXISTING ~spwi407.spl~ ~override/d5_conv2.spl~
		LPM spell_to_innate
	COPY_EXISTING ~spwi504.spl~ ~override/d5_conv3.spl~
		LPM spell_to_innate
	
	APPEND ~CLABMA06.2da~ ~MON_SUMM 		****        GA_D5_CONV1 ****        GA_D5_CONV1 ****        GA_D5_CONV2 ****        GA_D5_CONV2 ****        GA_D5_CONV3~ 
	
	//Description Updates
	ACTION_GET_STRREF str_conjurer conjurer
	OUTER_SPRINT old @110
	OUTER_SPRINT new @111
	OUTER_PATCH_SAVE conjurer ~%conjurer%~ BEGIN 
		REPLACE_TEXTUALLY ~%old%~ ~%new%~
	END 
	STRING_SET_EVALUATE str_conjurer ~%conjurer%~
	
	//adding skipped abilities to the bonus spells
	COPY_EXISTING ~QDMG+CON.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~SPWI309~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~SPWI407~ END 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=1 timing=9 STR_VAR resource= ~SPWI504~ END 
END 

//Invoker EE 
ACTION_IF (GAME_IS ~iwdee~ OR FILE_EXISTS_IN_GAME ~enginest.2da~) THEN BEGIN //iwdee 1.4+, Engine v2.0+

	//+20% Damage Bonus
	COPY ~TomeAndBlood/data/specialists/d5_pyroe.spl~ ~override/d5_invo1.spl~
	
	APPEND ~CLABMA07.2da~ ~DMGBONUS 		AP_D5_INVO1~
	
	//Description Update
	ACTION_GET_STRREF str_invoker invoker
	OUTER_SPRINT old @113
	OUTER_SPRINT new @114
	OUTER_PATCH_SAVE invoker ~%invoker%~ BEGIN 
		REPLACE_TEXTUALLY ~%old%~ ~%new%~
	END 
	STRING_SET_EVALUATE str_invoker ~%invoker%~
END 

//Illusioninst non-EE 
ACTION_IF ENGINE_IS ~tob~ THEN BEGIN //shadow step if not EEs
	COPY ~TomeAndBlood/data/specialists/spsd02.spl~ ~override~
		SAY NAME1 @6049
		SAY UNIDENTIFIED_DESC @6050
	COPY ~TomeAndBlood/data/specialists/spsd02b.bam~ ~override~
	COPY ~TomeAndBlood/data/specialists/spsd02c.bam~ ~override~
END 

//Ilusionist SR 
/*
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN 
	COPY ~TomeAndBlood/data/specialists/d5_illus.spl~ ~override/d5_illu1.spl~
END 
ELSE BEGIN 
	COPY ~TomeAndBlood/data/specialists/d5_illuv.spl~ ~override/d5_illu1.spl~
END 
*/
COPY ~TomeAndBlood/data/specialists/d5_illus.spl~ ~override/d5_illu1.spl~

//Necromancer "Rebuke Undead" ability
COPY ~TomeAndBlood/data/specialists/d5nctua.spl~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctub.eff~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctub.spl~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctuc.spl~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctuc.eff~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctux.spl~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctu.bam~ ~override~
COPY ~TomeAndBlood/data/specialists/d5nctux.bam~ ~override~

//Necromancer Undead Touch" abilities
COPY ~TomeAndBlood/data/specialists/d5nect1.spl~ ~override~
	SAY NAME1 @6251
	SAY IDENTIFIED_DESC @6252
COPY ~TomeAndBlood/data/specialists/d5nect2.spl~ ~override~
	SAY NAME1 @6253
	SAY IDENTIFIED_DESC @6254
COPY ~TomeAndBlood/data/specialists/d5nect3.spl~ ~override~
	SAY NAME1 @6255
	SAY IDENTIFIED_DESC @6256
COPY ~TomeAndBlood/data/specialists/d5nnct1.spl~ ~override~
	SAY NAME1 @6257
	SAY IDENTIFIED_DESC @6257
COPY ~TomeAndBlood/data/specialists/d5nnct2.spl~ ~override~
	SAY NAME1 @6258
	SAY IDENTIFIED_DESC @6258
COPY ~TomeAndBlood/data/specialists/d5nnct3.spl~ ~override~
	SAY NAME1 @6259
	SAY IDENTIFIED_DESC @6259
COPY ~TomeAndBlood/data/specialists/d5nect1.itm~ ~override~
	SAY NAME1 @6251
	SAY NAME2 @6251
	SAY IDENTIFIED_DESC @6251
COPY ~TomeAndBlood/data/specialists/d5nect2.itm~ ~override~
	SAY NAME1 @6253
	SAY NAME2 @6253
	SAY IDENTIFIED_DESC @6253
COPY ~TomeAndBlood/data/specialists/d5nect3.itm~ ~override~
	SAY NAME1 @6255
	SAY NAME2 @6255
	SAY IDENTIFIED_DESC @6255
COPY ~TomeAndBlood/data/specialists/d5_nect3.bam~ ~override~

APPEND ~hidespl.2da~ ~SPWI117	1	0	0~ //add chill touch to hidespl
APPEND ~hidespl.2da~ ~SPWI218	1	0	0~ //add ghoul touch to hidespl
APPEND ~hidespl.2da~ ~SPWI314	1	0	0~ //add vampiric touch to hidespl

// "_____ Touch" scrolls
ACTION_IF FILE_EXISTS_IN_GAME ~scrl82.itm~ BEGIN
  COPY_EXISTING ~scrl82.itm~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 opcode = 171 STR_VAR resource = ~d5nect1~ END
  BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~scrl1c.itm~ BEGIN
  COPY_EXISTING ~scrl1c.itm~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 opcode = 171 STR_VAR resource = ~d5nect2~ END
  BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~scrl1q.itm~ BEGIN
  COPY_EXISTING ~scrl1q.itm~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 opcode = 171 STR_VAR resource = ~d5nect3~ END
  BUT_ONLY
END

// get '_____ Touch' abilities from scrolls
COPY_EXISTING ~clabma03.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_D5NECT1~ ~****      ~
  REPLACE_TEXTUALLY ~GA_D5NECT2~ ~****      ~
  REPLACE_TEXTUALLY ~GA_D5NECT3~ ~****      ~
BUT_ONLY
	
/*
.
.
.
.
.
//Necromancer Character Creation Spells
COPY_EXISTING ~spwi110.spl~ ~override/QDNEC#1.spl~ 

ADD_SPELL  ~override/QDNEC#1.spl~ 2 1 TNB_NECRO_CC_I 
	SAY NAME1 @116 
	SAY UNIDENTIFIED_DESC @115
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers (and wildmages)
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 2 TNB_NECRO_CC_II 
	SAY NAME1 @117 
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 2
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 3 TNB_NECRO_CC_III
	SAY NAME1 @118
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 3
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 4 TNB_NECRO_CC_IV 
	SAY NAME1 @119 
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 4
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 

ADD_SPELL  ~override/QDNEC#1.spl~ 2 5 TNB_NECRO_CC_V 
	SAY NAME1 @120
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 5
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers (and wildmages)
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 6 TNB_NECRO_CC_VI 
	SAY NAME1 @121 
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 6
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 7 TNB_NECRO_CC_VII
	SAY NAME1 @122
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 7
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers 
	
ADD_SPELL  ~override/QDNEC#1.spl~ 2 8 TNB_NECRO_CC_VIII
	SAY NAME1 @123
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 8
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers

ADD_SPELL  ~override/QDNEC#1.spl~ 2 9 TNB_NECRO_CC_IX
	SAY NAME1 @124
	SAY UNIDENTIFIED_DESC @115
	WRITE_LONG 0x34 9
	WRITE_BYTE 0x25 7
	WRITE_LONG 0x1e ("0x00006fc0") //blocked to: all except necromancers (and wildmages)
	
	
//Remove Char Creation spells from Necro/Wild Mage 
//Create new spell to remove those 9 spells 
COPY ~TomeAndBlood/data/specialists/QDNEC#.spl~ ~override~

ACTION_FOR_EACH necspl IN ~TNB_NECRO_CC_I~ ~TNB_NECRO_CC_II~ ~TNB_NECRO_CC_III~ ~TNB_NECRO_CC_IV~ ~TNB_NECRO_CC_V~ ~TNB_NECRO_CC_VI~ ~TNB_NECRO_CC_VII~ ~TNB_NECRO_CC_VIII~ ~TNB_NECRO_CC_IX~ BEGIN 
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = EVAL ~%necspl%~ 
		RET spell_res
	END
	COPY_EXISTING ~QDNEC#.spl~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=9 STR_VAR resource= EVAL ~%spell_res%~ END  
END 

//also fixes wild mage, not destructively
APPEND ~CLABMA01.2da~ ~SPEC_FIX	 	AP_QDNEC#~
.
.
.
.
.
*/