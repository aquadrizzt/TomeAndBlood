// -----------------------------
// FAVOURED SOUL
// -----------------------------
INCLUDE ~TomeAndBlood/lib/fl#add_kit_ee.tpa~

//favoured soul
ADD_KIT ~QDFAVSL~
	~QDFAVSL 0 1 0 0 1 0 0 1 0~ //clasweap.2da
	~QDFAVSL 0 1 0 0 1 0 0 1 0 0 0 0 0 0 0 1 0 0 0 0 0 0 1 0 0 0 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~ //weapprof.2da 
	~QDFAVSL 0 0 0 0 0 9~ //abclasrq.2da 
	~QDFAVSL 0 0 0 0 0 0~ //abclsmod.2da
	~QDFAVSL 0 0 0 0 0 17~ //abdcdsrq.2da
	~QDFAVSL 0 0 0 0 0 15~ //abdcscrq.2da 
	~QDFAVSL 1 1 1 1 1 1 1 1 1~ //alignmnt.2da
	~QDFAVSL 0 0 0 0 0 0~ //dualclas.2da
	~TomeAndBlood/data/favoured_soul/QDFAVSL.2da~ //CLAB
	~K_S_H K_S_HE K_S_E K_S_D K_S_G K_S_HL K_S_HO~ 
	~0x00004000	19~ 
	~MA0~ 
	~* * * * * * * * * * * * * * * * * * * *~
	SAY @601
	SAY @602
	SAY @603

ACTION_IF GAME_IS ~bgee bg2ee iwdee eet~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@603)
		STR_VAR
			kit_name = QDFAVSL
	END
END	  

COPY ~TomeAndBlood/data/favoured_soul/QDFVSL1.spl~ ~override~ //prot evil 
     ~TomeAndBlood/data/favoured_soul/QDFVSL2.spl~ ~override~ //+2 saves, -1 spell 
     ~TomeAndBlood/data/favoured_soul/QDFVSLB.spl~ ~override~ //sphere: battle 
     ~TomeAndBlood/data/favoured_soul/QDFVSLD.spl~ ~override~ //sphere: death 
     ~TomeAndBlood/data/favoured_soul/QDFVSLL.spl~ ~override~ //sphere: life 
     ~TomeAndBlood/data/favoured_soul/QDFVSLN.spl~ ~override~ //sphere: nature 
     ~TomeAndBlood/data/favoured_soul/QDFVSLP.spl~ ~override~ //sphere: protection 
	 
//sphere selection dialog
ACTION_FOR_EACH prstspl IN  
 ~SPPR106~ //magic stone 
 ~SPPR108~ //remove fear  
 ~SPPR214~ //draw upon might  
 ~SPPR206~ //flame blade  
 ~SPPR312~ //strength of one  
 ~SPPR416~ //cloak of fear  
 ~SPPR507~ //champion's strength  
 ~SPPR603~ //blade barrier  
 ~SPPR703~ //gate 
 
 ~SPPR102~ //command  
 ~SPPR113~ //doom  
 ~SPPR208~ //hold person  
 ~SPPR213~ //spiritual hammer  
 ~SPPR301~ //animate dead  
 ~SPPR409~ //death ward  
 ~SPPR511~ //slay living  
 ~SPPR612~ //bolt of glory  
 ~SPPR715~ //unholy word  
 
 ~SPPR101~ //bless 
 ~SPPR103~ //cure light wounds 
 ~SPPR201~ //aid 
 ~SPPR212~ //slow poison 
 ~SPPR315~ //cure medium wounds 
 ~SPPR417~ //lesser restoration 
 ~SPPR514~ //mass cure 
 ~SPPR607~ //heal 
 ~SPPR712~ //resurrection 
 
 ~SPPR105~ //entangle 
 ~SPPR110~ //shillelagh 
 ~SPPR202~ //barkskin 
 ~SPPR207~ //goodberry 
 ~SPPR319~ //summon insects 
 ~SPPR410~ //call woodland beings 
 ~SPPR516~ //pixie dust 
 ~SPPR606~ //fire seeds
 ~SPPR704~ //nature's beauty  
 
 ~SPPR111~ //armor of faith 
 ~SPPR109~ //sanctuary 
 ~SPPR203~ //chant 
 ~SPPR211~ //silence, 15' radius 
 ~SPPR304~ //glyph of warding 
 ~SPPR406~ //defensive harmony 
 ~SPPR509~ //magic resistance 
 ~SPPR613~ //physical mirror
 ~SPPR701~ //shield of archons  
BEGIN 

COPY_EXISTING ~%prstspl%.spl~ ~.~ 
	INNER_PATCH_SAVE divinespell ~%SOURCE_FILE%~ BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~SPPR~ ~QDFV_~
	END 
	
	INNER_ACTION BEGIN 
		COPY_EXISTING ~%SOURCE_FILE%~ ~override/%divinespell%~ 
		WRITE_SHORT 0x1c ~1~ //change to arcane
		LPF ALTER_SPELL_EFFECT INT_VAR check_headers=1 match_opcode=321 STR_VAR resource= EVAL~%divinespell%~ END //update the "auto refresh" spell effects to the new spell 
	END 
	BUT_ONLY 
END 

COMPILE ~TomeAndBlood/data/favoured_soul/QDFVSLS.d~ 
COMPILE ~TomeAndBlood/data/favoured_soul/QDFVSLS.baf~

COPY ~TomeAndBlood/data/favoured_soul/QDFVSLS.cre~ ~override~ 
			WRITE_EVALUATED_ASCII 0x248 ~QDFVSLS~ 
			WRITE_EVALUATED_ASCII 0x2cc ~QDFVSLS~ 
			
COPY ~TomeAndBlood/data/favoured_soul/QDFVSLS.spl~ ~override~ 
	SAY NAME1 @604 
	SAY UNIDENTIFIED_DESC @605 
	LPF ALTER_SPELL_EFFECT 
		INT_VAR 
			match_opcode = 67
		STR_VAR 
			resource = EVALUATE_BUFFER ~QDFVSLS~ 
	END 
