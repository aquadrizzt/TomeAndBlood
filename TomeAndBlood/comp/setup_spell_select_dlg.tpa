// -----------------------------
// SPELL SELECT DIALOG 
// -----------------------------

COPY_EXISTING ~SPLSRCKN.2da~ ~override/QD_SPLSRCKN.2da~ //save a copy of the existing sorcerer spell progression 

COPY ~TomeAndBlood/data/spell_select/SPLSRCKN.2da~ ~override~ //set all sorcerer spells known to 0 

//framework for memorization dialog 
COPY ~TomeAndBlood/data/spell_select/QDSPLSOR.cre~ ~override~ 
			WRITE_EVALUATED_ASCII 0x248 ~QDSPLSOR~ 
			WRITE_EVALUATED_ASCII 0x2cc ~QDSPLSOR~

COPY ~TomeAndBlood/data/spell_select/QDSPLSOR.spl~ ~override~
			SAY NAME1 @1701
			SAY UNIDENTIFIED_DESC @1702
			LPF ALTER_SPELL_EFFECT 
				INT_VAR 
					match_opcode = 67
				STR_VAR 
					resource = ~QDSPLSOR~ 
			END 
			
COMPILE ~TomeAndBlood/data/spell_select/QDSPLSOR.baf~
COPY 	~TomeAndBlood/data/spell_select/QDSPLSOR.d~ ~override~ //easy to access copy of .d file to update

//SPELL PROGRESSION EVALUATION AND CONVERSION 
 
//level spells known snippet
<<<<<<<< QD/sor_spells_known.baf
	IF
		Level(LastSummonerOf(Myself),%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_sor_max_1","GLOBAL",%first%)
			SetGlobal("qd_sor_max_2","GLOBAL",%second%)
			SetGlobal("qd_sor_max_3","GLOBAL",%third%)
			SetGlobal("qd_sor_max_4","GLOBAL",%fourth%)
			SetGlobal("qd_sor_max_5","GLOBAL",%fifth%)
			SetGlobal("qd_sor_max_6","GLOBAL",%sixth%)
			SetGlobal("qd_sor_max_7","GLOBAL",%seventh%)
			SetGlobal("qd_sor_max_8","GLOBAL",%eighth%)
			SetGlobal("qd_sor_max_9","GLOBAL",%ninth%)
			Continue() 
	END 
>>>>>>>>
//level spells known ([second to] last defined level) snippet
<<<<<<<< QD/sor_spells_known_max.baf
	IF
		OR(2)
		Level(LastSummonerOf(Myself),%lvl%)
		LevelGT(LastSummonerOf(Myself),%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_sor_max_1","GLOBAL",%first%)
			SetGlobal("qd_sor_max_2","GLOBAL",%second%)
			SetGlobal("qd_sor_max_3","GLOBAL",%third%)
			SetGlobal("qd_sor_max_4","GLOBAL",%fourth%)
			SetGlobal("qd_sor_max_5","GLOBAL",%fifth%)
			SetGlobal("qd_sor_max_6","GLOBAL",%sixth%)
			SetGlobal("qd_sor_max_7","GLOBAL",%seventh%)
			SetGlobal("qd_sor_max_8","GLOBAL",%eighth%)
			SetGlobal("qd_sor_max_9","GLOBAL",%ninth%)
			Continue() 
	END 
>>>>>>>>
		
//Spells known progression reading
COPY ~override/QD_SPLSRCKN.2da~ ~override~ 
	COUNT_2DA_COLS num_cols
	COUNT_2DA_ROWS num_cols num_rows
	READ_2DA_ENTRIES_NOW ~r2en_sor_spl_kwn~ num_cols
	FOR (row = 0; row < r2en_sor_spl_kwn; row += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 1 first
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 2 second
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 3 third
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 4 fourth
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 5 fifth
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 6 sixth
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 7 seventh
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 8 eighth
		READ_2DA_ENTRY_FORMER ~r2en_sor_spl_kwn~ row 9 ninth
		INNER_ACTION BEGIN 
			ACTION_IF (row = %num_rows%-1) THEN BEGIN 
				EXTEND_BOTTOM ~QDSPLSOR.bcs~ ~QD/sor_spells_known_max.baf~ 
					lvl = (%row% + 1)
					EVAL 
			END 
			ELSE BEGIN 
				EXTEND_BOTTOM ~QDSPLSOR.bcs~ ~QD/sor_spells_known.baf~ 
					lvl = (%row% + 1)
					EVAL 
			END 
		END 
	END 
	
	
//Loading the appropriate spell lists 
ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN 
	INCLUDE ~TomeAndBlood/data/core/spell_list_base.tpa~
END 
ELSE BEGIN 
	INCLUDE ~TomeAndBlood/data/core/spell_list_sr.tpa~
END 

//SPELL DIALOG/SCRIPT SNIPPETS 
//script spell check snippet
<<<<<<<< QD/script_add_spell_check.baf
	IF
		NextTriggerObject(LastSummonerOf(Myself)) 
		HaveKnownSpellRES("%spl%") 
	THEN	
		RESPONSE #100 
			IncrementGlobal("%cur_lvl%","GLOBAL",1)
			Continue() 
	END 
>>>>>>>>
//script initiate dialog snippet
<<<<<<<< QD/script_spells_end.baf
	IF
		True()
	THEN
		RESPONSE #100
			StartDialog("QDSPLSOR",LastSummonerOf(Myself))
	END
>>>>>>>>
//dialog spell details snippet
<<<<<<<< QD/dialog_spell_info.d
	IF ~~ %spl% 
		SAY ~%desc%~ 
			++ @1703 DO ~ActionOverride(LastSummonerOf(Myself),AddSpecialAbility("%spl%")) ActionOverride(LastSummonerOf(Myself),Rest()) IncrementGlobal("%cur_lvl%","GLOBAL",1) SmallWait(1)~ + refresh
			++ @1704 + %lvl_menu%
			++ @1705 + main_menu
			++ @1706 + end
	END 
>>>>>>>>

ACTION_PHP_EACH arcanespell AS ind => res BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%ind%.spl~) BEGIN 
		COPY_EXISTING ~%ind%.spl~ ~override~	
			READ_STRREF NAME1 name
			READ_STRREF UNIDENTIFIED_DESC description
			READ_SHORT 0x1c spell_type 
			READ_SHORT 0x34 spell_lvl 
			READ_BYTE  0x25 spell_school
		ACTION_IF ( (~%ind%~ STRING_EQUAL ~SPWI124~) OR //NRD
					(~%ind%~ STRING_EQUAL ~SPWI222~) OR //Chaos Shield
					(~%ind%~ STRING_EQUAL ~SPWI723~) //Imp. Chaos Shield
					OR 
					((GAME_IS ~bgee~) AND 
						(~%ind%~ STRING_EQUAL ~SPWI126~)) //Dancing Lights
					OR 
					((GAME_IS ~bg2ee eet~) AND 
						(~%ind%~ STRING_EQUAL ~SPWI226~  OR //???
						 ~%ind%~ STRING_EQUAL ~SPWI228~)) //??? 
					OR 
					((GAME_IS ~iwdee~) AND 
						(~%ind%~ STRING_EQUAL ~SPWI106~ OR //blindness
						 ~%ind%~ STRING_EQUAL ~SPWI108~ OR //prot from petrification
						 ~%ind%~ STRING_EQUAL ~SPWI323~  OR //dispel magic
						 ~%ind%~ STRING_EQUAL ~SPWI516~  OR //conjure lesser fire 
						 ~%ind%~ STRING_EQUAL ~SPWI520~  OR //conjure lesser air
						 ~%ind%~ STRING_EQUAL ~SPWI521~  OR //conjure lesser earth
						 ~%ind%~ STRING_EQUAL ~SPWI527~  OR //conjure water 
						 ~%ind%~ STRING_EQUAL ~SPWI529~  OR //conjure earth
						 ~%ind%~ STRING_EQUAL ~SPWI530~  OR //animate dead
						 ~%ind%~ STRING_EQUAL ~SPWI819~)) //mon summ VI
					OR 
					((FILE_EXISTS_IN_GAME ~qdtnb_specialists.qd~) AND 
						(~%ind%~ STRING_EQUAL ~SPWI117~) OR //chill touch
						(~%ind%~ STRING_EQUAL ~SPWI218~) OR //ghoul touch	
						(~%ind%~ STRING_EQUAL ~SPWI314~)) //vampiric touch	
					OR 
					((FILE_EXISTS_IN_GAME ~qdtnb_familiar.qd~) AND 
						(~%ind%~ STRING_EQUAL ~SPWI123~)) //find familiar	
					OR 
					((FILE_EXISTS_IN_GAME ~qdtnb_spelltweaks.qd~) AND
						((FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) AND 
							(~%ind%~ STRING_EQUAL ~SPWI102~ OR //armor
							 ~%ind%~ STRING_EQUAL ~SPWI201~ OR //blur
							 ~%ind%~ STRING_EQUAL ~SPWI120~ OR //reflected image
							 ~%ind%~ STRING_EQUAL ~SPWI212~ OR //mirror image
							 ~%ind%~ STRING_EQUAL ~SPWI811~ OR //symbol fear
							 ~%ind%~ STRING_EQUAL ~SPWI916~ OR //shapechange
							 ~%ind%~ STRING_EQUAL ~SPWI915~ OR //black blade of disaster
							 ~%ind%~ STRING_EQUAL ~SPWI914~)) //energy drain
						OR 
						((NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) AND 
							(~%ind%~ STRING_EQUAL ~SPWI722~ OR //limited wish
							 ~%ind%~ STRING_EQUAL ~SPWI811~ OR //symbol fear
							 ~%ind%~ STRING_EQUAL ~SPWI915~ OR //shapechange
							 ~%ind%~ STRING_EQUAL ~SPWI916~)) //black blade of disaster 
					)  
					OR 
					((FILE_EXISTS_IN_GAME ~qdtnb_metamagic.qd~) AND 
						(~%ind%~ STRING_EQUAL ~SPWI420~) OR //Minor Sequencer		
						(~%ind%~ STRING_EQUAL ~SPWI617~) OR //Contingency	
						(~%ind%~ STRING_EQUAL ~SPWI710~) OR //Spell Sequencer		
						(~%ind%~ STRING_EQUAL ~SPWI809~) OR //Spell Trigger	
						(~%ind%~ STRING_EQUAL ~SPWI908~)) //Chain Contingency	
					OR 
					((FILE_EXISTS_IN_GAME ~qdtnb_l1cantrips.qd~) AND
						((FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) AND 
							(~%ind%~ STRING_EQUAL ~SPWI109~ OR //dimension jump
							 ~%ind%~ STRING_EQUAL ~SPWI110~ OR //identify
							 ~%ind%~ STRING_EQUAL ~SPWI111~ OR //true strike
							 ~%ind%~ STRING_EQUAL ~SPWI116~ OR //sleep
							 ~%ind%~ STRING_EQUAL ~SPWI220~ OR //word sleep
							 ~%ind%~ STRING_EQUAL ~SPWI112~ OR //chromatic orb?
							 ~%ind%~ STRING_EQUAL ~SPWI315~)) //wraithform
						OR 
						((NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) AND 
							(~%ind%~ STRING_EQUAL ~SPWI107~ OR //friends
							 ~%ind%~ STRING_EQUAL ~SPWI108~ OR //prot. petrification
							 ~%ind%~ STRING_EQUAL ~SPWI110~ OR //identify
							 ~%ind%~ STRING_EQUAL ~SPWI114~ OR //shield
							 ~%ind%~ STRING_EQUAL ~SPWI116~ OR //sleep
							 ~%ind%~ STRING_EQUAL ~SPWI220~ OR //word sleep
							 ~%ind%~ STRING_EQUAL ~SPWI112~ OR //chromatic orb?
							 ~%ind%~ STRING_EQUAL ~SPWI315~)) //wraithform
					)						
				) BEGIN 
			//exclusion -> skips this spell completely 
		END 
		ELSE BEGIN 
			//inclusion -> spell is added to dialog 
			EXTEND_BOTTOM ~QDSPLSOR.bcs~ ~QD/script_add_spell_check.baf~ 
				SPRINT spl ~%ind%~ 
				SPRINT cur_lvl ~qd_sor_cur_~^~%spell_lvl%~
				EVAL 
			ACTION_IF (FILE_EXISTS_IN_GAME ~qdtnb_specialists.qd~ AND FILE_EXISTS_IN_GAME ~qdtnb_revenant.qd~ AND %spell_school% = 7) THEN BEGIN 
				COPY_EXISTING ~override/QDSPLSOR.d~ ~override~ 
					SPRINT name "%name%"
					SPRINT desc "%description%"
					SPRINT cur_lvl "qd_sor_cur_"^"%spell_lvl%"
					SPRINT spl "%ind%"
					SPRINT lvl "%spell_lvl%"
					SPRINT lvl_menu "level_"^"%lvl%"
					APPEND_FILE_EVALUATE  ~QD/dialog_spell_info.d~ 
					REPLACE_TEXTUALLY ~//Marker %spell_lvl%~ ~~~~~+ ~NextTriggerObject(LastSummonerOf(Myself)) !HaveKnownSpellRES("%ind%") Kit(LastSummonerOf(Myself),"QDREVSR")~ +~%name%~ + %ind% 
									   //Marker %spell_lvl%~~~~~
			END 
			ELSE BEGIN 
				COPY_EXISTING ~override/QDSPLSOR.d~ ~override~ 
					SPRINT name "%name%"
					SPRINT desc "%description%"
					SPRINT cur_lvl "qd_sor_cur_"^"%spell_lvl%"
					SPRINT spl "%ind%"
					SPRINT lvl "%spell_lvl%"
					SPRINT lvl_menu "level_"^"%lvl%"
					APPEND_FILE_EVALUATE  ~QD/dialog_spell_info.d~ 
					REPLACE_TEXTUALLY ~//Marker %spell_lvl%~ ~~~~~+ ~NextTriggerObject(LastSummonerOf(Myself)) !HaveKnownSpellRES("%ind%")~ +~%name%~ + %ind% 
									   //Marker %spell_lvl%~~~~~
			END 
		END 
	END 
END 

EXTEND_BOTTOM ~QDSPLSOR.bcs~ ~QD/script_spells_end.baf~ EVAL 
COMPILE ~override/QDSPLSOR.d~ 

COPY ~TomeAndBlood/data/spell_select/QDSPSRK.spl~ ~override~
	//adding craft wands to mages and sorcerers
INCLUDE ~TomeAndBlood/data/core/tnb_kit_list.tpa~ 
	//mages get scribe scroll, potion and craft wand
ACTION_PHP_EACH tnb_kit_list AS kitinfo => kitclab BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%kitclab%.2da~) BEGIN 
		ACTION_IF (kitinfo_1 = 0) BEGIN 
			APPEND ~%kitclab%.2da~ ~MEMORIZE AP_QDSPSRK~
		END 
		ACTION_IF (kitinfo_1 = 19) BEGIN 
			APPEND ~%kitclab%.2da~ ~MEMORIZE GA_QDSPLSOR~
		END 
	END 
END  
