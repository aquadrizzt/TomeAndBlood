// -----------------------------
// SORCERER: MAGUS
// -----------------------------
INCLUDE ~TomeAndBlood/lib/fl#add_kit_ee.tpa~
//Note: This component uses resources provided by Mike1072. All such resources are used with the permission of the original author. 

ADD_KIT ~QDMAGUS~
	~QDMAGUS 2 2 0 2 2 2 2 2~ //clasweap.2da
	~QDMAGUS 2 2 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 2 2 2 2 0 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~ //weapprof.2da 
	~QDMAGUS 9 0 0 0 0 9~ //abclasrq.2da 
	~QDMAGUS 0 0 0 0 0 0~ //abclsmod.2da
	~QDMAGUS 17 0 0 0 0 17~ //abdcdsrq.2da
	~QDMAGUS 15 0 0 0 0 15~ //abdcscrq.2da 
	~QDMAGUS 1 1 1 1 1 1 1 1 1~ //alignmnt.2da
	~QDMAGUS 0 0 0 0 0 0~ //dualclas.2da
	~TomeAndBlood/data/magus/QDMAGUS.2da~ //CLAB
	~K_S_H K_S_HE K_S_E K_S_D K_S_G K_S_HL K_S_HO~ 
	~0x00004000	19~ 
	~FM0~ 
	~* * * * * * * * * * * * * * * * * * * *~
	SAY @301
	SAY @302
	SAY @303

ACTION_IF GAME_IS ~bgee bg2ee iwdee eet~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@303)
		STR_VAR
			kit_name = QDMAGUS
			hpclass = HPPRS
	END
END 

COPY ~TomeAndBlood/data/magus/QDMGUS1.SPL~ ~override~ //magus: can't use shields; -2 casting speed; -1 spell slot 

//Armor and Shield Lookup Tables (by Mike1072 for Item Revisions)
INCLUDE ~TomeAndBlood/data/armored_casting/armor_list.tpa~

//Effect that allows arcane casting
COPY ~TomeAndBlood/data/armored_casting/QDARMCST.EFF~ ~override~ //allowing effect 

//Get Magus kit ID
COPY_EXISTING - ~kitlist.2da~ ~override~
	COUNT_2DA_COLS num_cols
		READ_2DA_ENTRIES_NOW ~r2en_kitlist~ num_cols
		FOR (row = 1; row < r2en_kitlist; row += 1) BEGIN
			READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 1 kitname
			PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~QDMAGUS~) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 9 magus_id
			END
		END 

//Patch armors to allow for casting by Magus
ACTION_PHP_EACH armor AS ind => res BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%ind%.itm~) BEGIN 
		COPY_EXISTING ~%ind%.itm~ ~override~ 
			PATCH_IF (SOURCE_SIZE>0x71) BEGIN
				LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 145 END 
				LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 60 END 
			END	
			
			READ_BYTE   0x20 "mage"
			WRITE_BYTE 0x20 ("%mage%" BAND "0b11111011")
			is_elven = (res >> 5) & 1
			armor_type = res & 7 
			
			PATCH_IF (is_elven = 0 && (armor_type = 1 || armor_type = 2 || armor_type = 3 || armor_type = 4 || armor_type = 5 || armor_type = 6 || armor_type = 7)) BEGIN 
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 timing = 2 parameter1 = EVAL ~%magus_id%~ parameter2 = 9 STR_VAR resource = EVAL ~QDARMCST~ END //MAGUS 
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 145 target = 1 timing = 2 parameter1 = 0 parameter2 = 0 END //added at end to ensure proper effect order 					
			END
	END
END 

//Allow Mages and Sorcerers to equip any weapon/armor/shield/helmet (must still meet class/race/ability/alignment reqs)
	COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			READ_SHORT 0x31 "prof" ELSE 0
			READ_SHORT 0x1c "type" ELSE 0
			PATCH_IF ("%prof%" = 89 || //bastard sword
					  "%prof%" = 90 || //long sword
					  "%prof%" = 91 || //short sword
					  "%prof%" = 92 || //axe
					  "%prof%" = 93 || //2h sword
					  "%prof%" = 94 || //katana
					  "%prof%" = 95 || //scimitar 
					  "%prof%" = 97 || //war hammer
					  "%prof%" = 98 || //spear
					  "%prof%" = 99 || //halberd
					  "%prof%" = 100 || //flail/morningstar
					  "%prof%" = 101 || //mace
					  "%prof%" = 103 || //xbow
					  "%prof%" = 104 || //long bow
					  "%prof%" = 105 || //shortbow
					  "%prof%" = 115 || //club
					  "%type%" = 31 || //xbow bolts
					  "%type%" = 5  || //arrows
					  "%type%" = 2  || //armor
					  "%type%" = 7  || //helms
					  "%type%" = 12) BEGIN //shields
						READ_BYTE 0x1e "bard"
						READ_BYTE 0x1f "fighter"
						PATCH_IF (("%fighter%" BAND "0b00001000") = "0b00000000") BEGIN 
							READ_BYTE   0x20 "mage"
							WRITE_BYTE 0x20 ("%mage%" BAND "0b11111011")
						END	
			END
		END
	BUT_ONLY
